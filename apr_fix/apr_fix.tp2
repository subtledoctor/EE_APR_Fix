BACKUP ~apr_fix/backup~
AUTHOR ~SubtleD~


LANGUAGE
  ~English~
  ~english~
  ~apr_fix/language/english/setup.tra~
LANGUAGE
  ~Polski (Translation by Cahir)~
  ~polish~
  ~apr_fix/language/polish/setup.tra~


BEGIN @1
DESIGNATED 1

//___changed all fired/thrown weapons to use "replace with used up"________________
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
		READ_SHORT 0x1c ammo
		PATCH_IF (%ammo% = 24 || %ammo% = 31 || %ammo% = 14 || %ammo% = 5 || %ammo% = 16 || %ammo% = 21 || %ammo% = 25) BEGIN
			READ_SHORT 0x96 used_up
			PATCH_IF (%used_up% = 1) BEGIN    // if currently set to "item vanishes" ...
				WRITE_SHORT 0x96 2            // ...then set it to "replaced with used up
				WRITE_LONG 0x10 0             // and make sure that...
				WRITE_LONG 0x14 0             // ..."used up item" is set to "none"
			END
		END
	END
BUT_ONLY
//_________________________________________________________________________________

/*
//___revert sol's searing orb to original behavior_________________________________
COPY_EXISTING ~sorb.itm~ ~override~
	WRITE_SHORT 0x96 1
BUT_ONLY
//_________________________________________________________________________________
*/

//___change MMM, energy blades, & searign orb to create items in inventory_________
COPY_EXISTING ~spwi325.spl~ ~override~    // melf's minutes meteors
			~spwi920.spl~ ~override~      // energy blades
			~sppr721.spl~ ~override~      // energy blades
			~sppr614.spl~ ~override~      // searing orb
	LPF ALTER_EFFECT INT_VAR match_opcode=111 opcode=255 timing=0 duration=0 END
BUT_ONLY
//_________________________________________________________________________________


//___set characteristics and usability for MMM & energy blades_____________________
COPY_EXISTING ~melfmet.itm~ ~override~ // melf's meteors
	WRITE_LONG 0x10 0
	WRITE_LONG 0x14 0
	WRITE_BYTE 0x18 (THIS | 0b00000100) // sets "droppable" flag to checked
	WRITE_BYTE 0x18 (THIS & 0b11111101) // added in v1.1 to ensure "2-handed" flag is unchecked
	WRITE_BYTE 0x1e (THIS | 0b10000000) // unusable by cleric
	WRITE_BYTE 0x1f (THIS | 0b01011110) // unusable by F/C, F/D, fighter, C/R, and C/T
	WRITE_BYTE 0x18 (THIS | 0b01110010) // unusable by thief, ranger, paladin, and F/T
	WRITE_BYTE 0x18 (THIS | 0b01100000) // unusable by druid and monk
BUT_ONLY

COPY_EXISTING ~eneblade.itm~ ~override~ // energy blades
	WRITE_LONG 0x10 0
	WRITE_LONG 0x14 0
	WRITE_BYTE 0x18 (THIS | 0b00000100) // sets "droppable" flag to checked
	WRITE_BYTE 0x18 (THIS & 0b11111101) // added in v1.1 to ensure "2-handed" flag is unchecked
	WRITE_BYTE 0x1e (THIS | 0b01000000) // unusable by bard
	WRITE_BYTE 0x1f (THIS | 0b00001000) // unusable by fighter
	WRITE_BYTE 0x18 (THIS | 0b01110010) // unusable by thief, ranger, paladin, and F/T
	WRITE_BYTE 0x18 (THIS | 0b00100000) // unusable by monk
BUT_ONLY

COPY_EXISTING ~sorb.itm~ ~override~ // searing orb
	WRITE_LONG 0x10 0
	WRITE_LONG 0x14 0
	WRITE_BYTE 0x18 (THIS | 0b00000100) // sets "droppable" flag to checked
	WRITE_BYTE 0x18 (THIS & 0b11111101) // added in v1.1 to ensure "2-handed" flag is unchecked
	WRITE_BYTE 0x1e (THIS | 0b01000000) // unusable by bard
	WRITE_BYTE 0x1f (THIS | 0b00101000) // unusable by F/M and fighter
	WRITE_BYTE 0x18 (THIS | 0b01111111) // unusable by thief, ranger, paladin, M/T, mage, F/T, and F/M/T
	WRITE_BYTE 0x18 (THIS | 0b00100000) // unusable by monk
//_________________________________________________________________________________


//___set searing orb description___________________________________________________
ACTION_IF ENGINE_IS ~tob bg2ee~ BEGIN
	STRING_SET 38580 @10
END
//_________________________________________________________________________________
